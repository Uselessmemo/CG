<html>

<head>
    <title>lab8</title>
    <script type='text/javascript' src='js/gl-matrix-min.js'></script>
    <script type='text/javascript' src='js/utils.js'></script>

    <script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;

    uniform mat4 uMVMatrix; 
    uniform mat4 uPMatrix; 
    uniform mat4 uNMatrix; 

    varying vec3 vNormal;
    varying vec3 vEyeVec;

    void main(void) {
        //Transformed vertex position
        vec4 vertex = uMVMatrix * vec4(aVertexPosition, 1.0);
        
        //Transformed normal position
        vNormal = vec3(uNMatrix * vec4(aVertexNormal, 1.0));

        //Vector Eye
        vEyeVec = -vec3(vertex.xyz);
        
        //Final vertex position
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    }
    </script>

    <script id="shader-fs" type="x-shader/x-fragment">
    #ifdef GL_ES
    precision highp float;
    #endif

    uniform float uShininess;        //shininess
    uniform vec3 uLightDirection;  //light direction

    uniform vec4 uLightAmbient;      //light ambient property
    uniform vec4 uLightDiffuse;      //light diffuse property 
    uniform vec4 uLightSpecular;     //light specular property

    uniform vec4 uMaterialAmbient;  //object ambient property
    uniform vec4 uMaterialDiffuse;   //object diffuse property
    uniform vec4 uMaterialSpecular;  //object specular property

    varying vec3 vNormal;
    varying vec3 vEyeVec;

    void main(void)
    {
        vec3 L = normalize(uLightDirection);
        vec3 N = normalize(vNormal);
        
        //Lambert's cosine law
        float lambertTerm = dot(N,-L);
        
        //Ambient Term
        vec4 Ia = uLightAmbient * uMaterialAmbient;
        
        //Diffuse Term
        vec4 Id = vec4(0.0,0.0,0.0,1.0);
        
        //Specular Term
        vec4 Is = vec4(0.0,0.0,0.0,1.0);
        
        if(lambertTerm > 0.0) //only if lambertTerm is positive
        {
            Id = uLightDiffuse * uMaterialDiffuse * lambertTerm; //add diffuse term
            
            vec3 E = normalize(vEyeVec);
            vec3 R = reflect(L, N);
            float specular = pow( max(dot(R, E), 0.0), uShininess);
            
            Is = uLightSpecular * uMaterialSpecular * specular; //add specular term 
        }
        
        //Final color
        vec4 finalColor = Ia + Id + Is;
        finalColor.a = 1.0;
        
        gl_FragColor = finalColor;
    }
    </script>



    <script id='code-js' type="text/javascript" src="js/script.js">
    </script>
</head>

<body onLoad='runWebGLApp()'>

    <div id='contents'>
        <div id='canvasContainer'>
            <canvas id='canvas-element-id' width='800' height='800' style="border: 1px solid black;"></canvas>
        </div>
    </div>

</body>

</html>